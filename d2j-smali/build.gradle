apply plugin: 'antlr'

dependencies {
    implementation "org.antlr:antlr4-runtime:${project.property('antlr4Version')}"
    implementation project(':dex-reader-api')
    implementation project(':dex-reader')
    antlr "org.antlr:antlr4:${project.property('antlr4Version')}"
    implementation project(':d2j-base-cmd')
    implementation project(':dex-writer')
    testImplementation "com.android.tools.smali:smali-baksmali:${project.property('baksmaliVersion')}"
}

generateGrammarSource {
    arguments += ['-no-listener', '-visitor', '-package', 'com.googlecode.d2j.smali.antlr4']
}

sourceSets {
    test.output.resourcesDir = "build/classes/test"
    main.antlr.srcDirs = ['src/main/antlr4']
}

// From https://github.com/gradle/gradle/issues/19555#issuecomment-1593252653
sourceSets.configureEach {
    var generateGrammarSource = tasks.named(getTaskName("generate", "GrammarSource"))
    java.srcDir(generateGrammarSource.map { files() })
}
