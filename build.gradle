plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'
    group = 'com.googlecode.d2j'
    version = '2.1-SNAPSHOT'

    sourceCompatibility = targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: '*.jar')
    }

    [compileJava, compileTestJava]*.options.collect { options -> options.encoding = 'UTF-8' }
}

subprojects {
    dependencies {
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    }
}

mainClassName = "com.googlecode.dex2jar.tools.Dex2jarCmd"
archivesBaseName = "dex2jar"

dependencies {
    implementation project(':dex-tools')
}

shadowJar {
    archiveClassifier.set(null)
    archiveVersion.set("")
}